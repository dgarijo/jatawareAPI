#+ method: GET
#+ summary: This query returns the configurations compatible with input(s) of the given model configuration URI
#+ defaults:
#+   - config: https://w3id.org/mint/instance/FSC_config

PREFIX model: <https://w3id.org/mint/modelCatalog#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>

SELECT ?comp_config ?label ?description (GROUP_CONCAT(DISTINCT ?var;SEPARATOR=', ') AS ?comp_var)
from <https://w3id.org/mint/instance/modelservice_graph>
WHERE {
  ?_config_iri model:hasInput/model:hasPresentation/model:hasStandardVariable ?var.

  ?comp_config a model:ModelConfiguration.
  ?comp_config model:hasOutput/model:hasPresentation/model:hasStandardVariable ?var.
  ?comp_config rdfs:label ?label.
  OPTIONAL{?comp_config dct:description ?description.}.

  FILTER(?comp_config != ?_config_iri)
}GROUP BY ?comp_config ?label ?description